allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

val newBuildDir: Directory =
    rootProject.layout.buildDirectory
        .dir("../../build")
        .get()
rootProject.layout.buildDirectory.value(newBuildDir)

subprojects {
    val newSubprojectBuildDir: Directory = newBuildDir.dir(project.name)
    project.layout.buildDirectory.value(newSubprojectBuildDir)
}
subprojects {
    project.evaluationDependsOn(":app")

    // 旧版 Flutter 插件(如 isar_flutter_libs 3.x)未声明 namespace,
    // AGP 8.11+ 强制要求,这里为缺失 namespace 的 library 模块兜底注入。
    project.plugins.withId("com.android.library") {
        project.extensions.configure<com.android.build.gradle.LibraryExtension>("android") {
            if (namespace.isNullOrBlank()) {
                namespace = "com.privateagent.plugin." + project.name.replace(Regex("[^a-zA-Z0-9_.]"), "_")
            }
        }
    }

    // isar_flutter_libs 3.1.0 硬编码 compileSdkVersion 30,但其资源引用了
    // android:lStar(API 33+ 才存在),低版本 SDK 下 AAPT 链接报错。
    // 需在插件自己的 build.gradle(android{} 块)执行完后、AGP 固化 DSL 之前改掉,
    // 因此在 root 的 subprojects 阶段先注册 afterEvaluate(先于 AGP 注册的回调执行)。
    if (!project.state.executed) {
        project.afterEvaluate {
            project.plugins.withId("com.android.library") {
                project.extensions.configure<com.android.build.gradle.LibraryExtension>("android") {
                    if ((compileSdk ?: 0) < 33) {
                        compileSdk = 35
                    }
                }
            }
        }
    }
}

tasks.register<Delete>("clean") {
    delete(rootProject.layout.buildDirectory)
}
